buildscript {
	repositories {
		maven {
			url = "https://maven.minecraftforge.net"
		}
	}
	dependencies {
		classpath("com.anatawa12.forge:ForgeGradle:2.3-1.0.+") {
			changing = true
		}
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
	maven {
		name = "Curse Maven"
		url "https://www.cursemaven.com"
	}
}

configurations {
	provided
	shade
	compile.extendsFrom shade
}

idea {
	module {
		scopes.PROVIDED.plus += [configurations.provided]
	}
}

jar {
	configurations.shade.each { dep ->
		from(project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}

	from sourceSets.api.output
}

version = "${mc_version}-${mod_version}"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	
	replace "1.12.2-9999.9999.9999z", project.version
	replaceIn "ModConstants.java"
	
	makeObfSourceJar = false
	
	if (project.hasProperty('mappings_version'))
		mappings = project.mappings_version
}

dependencies {
	deobfCompile "curse.maven:biomes-o-plenty-220318:2842510"
	deobfCompile "curse.maven:dramatictrees-637540:4398759"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
